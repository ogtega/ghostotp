apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 30
    ndkVersion Versions.ndk
    buildToolsVersion Versions.build_tools

    defaultConfig {
        applicationId "de.tolunla.ghostotp"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 2
        versionName "0.4.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

dependencies {
    implementation project(path: ':steamguard')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    /**
     * Kotlin Standard Library JDK 8 extension
     */
    implementation Dependencies.kotlin_stdlib
    /**
     * Support Library Packages
     */
    implementation Dependencies.appcompat
    implementation Dependencies.constraint_layout
    implementation Dependencies.material
    /**
     * Lifecycle Components (Lifecycle, LiveData & ViewModel)
     */
    implementation Dependencies.lifecycle_common
    implementation Dependencies.lifecycle_ext
    implementation Dependencies.lifecycle_viewmodel
    /**
     * Room Persistence Library
     */
    kapt Annotations.room
    implementation Dependencies.room
    implementation Dependencies.room_runtime
    /**
     * Navigation Components
     */
    implementation Dependencies.navigation_fragment
    implementation Dependencies.navigation_ui
    /**
     * Barcode Library
     */
    implementation Dependencies.camerax_core
    implementation Dependencies.camerax_lifecycle
    implementation Dependencies.camerax_view
    implementation Dependencies.mlkit_barcode
    /**
     * Miscellaneous Libraries
     */
    implementation Dependencies.androidx
    implementation Dependencies.fragment
    implementation Dependencies.preference
    implementation Dependencies.work
    implementation Dependencies.apache_codec
    /**
     * Test Dependencies
     */
    testImplementation Testing.junit
    testImplementation Testing.room

    androidTestImplementation Testing.android_junit
    androidTestImplementation Testing.android_espresso
    androidTestImplementation Testing.android_work
}
